apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion rootProject.ext.compileSdk
    buildToolsVersion rootProject.ext.buildTools

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId 'com.turlir.abakgists'
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode 1
        versionName '1.0'

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            def propFile = file("keys/keystore.properties")
            def prop = new Properties()
            prop.load(new FileInputStream(propFile))

            keyAlias prop['keyAlias']
            keyPassword prop['keyPassword']
            storeFile file(prop['storeFile'])
            storePassword prop['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(path: ':tokenizelayout')

    implementation 'com.jakewharton:butterknife:8.7.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.7.0'

    implementation "com.android.support:appcompat-v7:$rootProject.ext.support"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.support"
    implementation "com.android.support:design:$rootProject.ext.support"
    implementation 'com.squareup.picasso:picasso:2.5.2'

    implementation 'com.google.dagger:dagger:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    implementation "io.reactivex.rxjava2:rxjava:2.1.17"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'

    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    implementation "android.arch.persistence.room:runtime:1.1.1"
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
    implementation "android.arch.persistence.room:rxjava2:1.1.1"

    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.jakewharton.timber:timber:4.5.1'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'

    implementation "com.android.support:support-vector-drawable:$rootProject.ext.support"
    implementation "com.android.support:animated-vector-drawable:$rootProject.ext.support"
    implementation 'jp.wasabeef:recyclerview-animators:2.2.7'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.8.9'
    testImplementation 'org.robolectric:robolectric:3.5.1'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testImplementation 'com.github.fabioCollini:DaggerMock:0.8.4'
    testImplementation "android.arch.persistence.room:testing:1.1.1"
}
